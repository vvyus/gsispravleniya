
#Область СлужебныйПрограммныйИнтерфейс
#Область ТекущиеДела
&После("ПриЗаполненииСпискаТекущихДел")
Процедура ГСИ_ПриЗаполненииСпискаТекущихДел(ТекущиеДела) Экспорт
	ГСИ_ДобавитьТекущееДелоДниРожденияСотрудников(ТекущиеДела);
КонецПроцедуры
#КонецОбласти
#КонецОбласти                                     

#Область СлужебныеПроцедурыИФункции

Процедура ГСИ_ДобавитьТекущееДелоДниРожденияСотрудников(ТекущиеДела)
	
	Если Не ПравоДоступа("Чтение", Метаданные.Справочники.Сотрудники) Тогда
		Возврат; // Нет прав на чтение сотрудников - не может увидеть
	КонецЕсли;

	Если Не (ПравоДоступа("Добавление", Метаданные.Документы.ПриемНаРаботу) 
		Или ПравоДоступа("Добавление", Метаданные.Документы.ПриемНаРаботуСписком)) Тогда
		Возврат; //  Не может выполнить текущее дело - принять на работу не принятых.
	КонецЕсли;
	
	МодульТекущиеДелаСервер = ОбщегоНазначения.ОбщийМодуль("ТекущиеДелаСервер");
	Разделы = МодульТекущиеДелаСервер.РазделыДляОбъекта(Метаданные.Справочники.Сотрудники.ПолноеИмя());
	Если Разделы.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;

	СотрудникиДр = ГСИ_СотрудникиСОтношениями();
	СотрудникиДр.Сортировать("ГоловнаяОрганизация, Наименование");

	Обход = Обработки.ОбходКоллекции.НовыйОбход(СотрудникиДр);
	Пока Обход.СледующийПоЗначениюПоля("ГоловнаяОрганизация") Цикл
		Сотрудников = 0;
		Пока Обход.Следующий() Цикл
			Сотрудников = Сотрудников + 1;
		КонецЦикла;
		Для Каждого Раздел Из Разделы Цикл
			Если Раздел.Имя<>"Кадры" Тогда
				Продолжить;
			КонецЕсли;	
			Дело = ТекущиеДела.Добавить();
			ИдентификаторПрофиля = "СотрудникиДниРождений_"
				+ ЗарплатаКадрыРасширенныйКлиентСервер.УникальноеИмяРеквизита();
			Дело.Идентификатор = ИдентификаторПрофиля;	
			Дело.ЕстьДела       = Сотрудников > 0;
			Дело.Важное         = Истина;
			Дело.Владелец       = Раздел;
			Дело.Представление  = СтрШаблон(НСтр("ru = 'Дни рождения в %1'"), 
				Обход.ТекущиеДанные().ГоловнаяОрганизация);
			Дело.Количество		= Сотрудников;
			Дело.Подсказка      = СтрШаблон(
				НСтр("ru = '%1. Необходимо поздравить сотрудников!'"),
				СтроковыеФункцииКлиентСервер.СтрокаСЧисломДляЛюбогоЯзыка(
					НСтр("ru = ';%1 сотрудник день рождения;;%1 сотрудника день рождения;%1 сотрудников дни рождения;%1 сотрудника день рождения'"), 
					Сотрудников));
			Если Сотрудников > 0 Тогда
				Дело.ПараметрыФормы = Новый Структура("Организация", Обход.ТекущиеДанные().ГоловнаяОрганизация);
				Дело.Форма = Метаданные.Справочники.Сотрудники.Формы.ФормаСписка.ПолноеИмя();
			КонецЕсли;
			Для Каждого СтрСотр Из СотрудникиДр Цикл
				Дело = ТекущиеДела.Добавить();
				Дело.Идентификатор = "СотрудникДеньРождения_"+ ЗарплатаКадрыРасширенныйКлиентСервер.УникальноеИмяРеквизита();
				Дело.ЕстьДела      = Истина;
				Дело.Важное        = Истина; 
				Наименование= СокрП(СтрСотр.Наименование);
				//Дело.Представление = СтрШаблон("%1%2",ДополнитьСтрокуПробелами(Наименование,35),Формат(СтрСотр.ДатаРождения,"ДЛФ=Д"));
				Дело.Представление = ГСИ_ДополнитьСтрокуПробелами(Наименование,35)+Формат(СтрСотр.ДатаРождения,"ДЛФ=Д");
				Дело.Форма         = "Справочник.Сотрудники.ФормаОбъекта";
				Дело.ПараметрыФормы= Новый Структура("Ключ", СтрСотр.Сотрудник);
				Дело.Владелец      = ИдентификаторПрофиля;
				
			КонецЦикла;	
		КонецЦикла;
	КонецЦикла;

КонецПроцедуры 
 
Функция ГСИ_СотрудникиСОтношениями(ГоловнаяОрганизация = Неопределено)
	Период=ТекущаяДата();
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("ГоловнаяОрганизация", ГоловнаяОрганизация);
	Запрос.УстановитьПараметр("ДатаОтсчета", Период); 
	Запрос.УстановитьПараметр("РазностьДней", 3);                      
	Запрос.УстановитьПараметр("ТекГод", Год(Период)); 
	Запрос.УстановитьПараметр("ДатаУвольнения", Дата(1,1,1)); 
	
	Запрос.Текст =                               
		"ВЫБРАТЬ
		|	ФизическиеЛица.Ссылка КАК Ссылка,
		|	ФизическиеЛица.ДатаРождения КАК ДатаРождения
		|ПОМЕСТИТЬ ВТФизическиеЛица
		|ИЗ
		|	Справочник.ФизическиеЛица КАК ФизическиеЛица
		|ГДЕ
		|	РАЗНОСТЬДАТ(ДОБАВИТЬКДАТЕ(ФизическиеЛица.ДатаРождения, ГОД, &ТекГод - ГОД(ФизическиеЛица.ДатаРождения)), &ДатаОтсчета, ДЕНЬ) <= 0
		|	И РАЗНОСТЬДАТ(ДОБАВИТЬКДАТЕ(ФизическиеЛица.ДатаРождения, ГОД, &ТекГод - ГОД(ФизическиеЛица.ДатаРождения)), &ДатаОтсчета, ДЕНЬ) >= -&РазностьДней
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Сотрудники.Ссылка КАК Сотрудник,
		|	ФизическиеЛица.Ссылка КАК ФизическоеЛицо,
		|	ФизическиеЛица.ДатаРождения КАК ДатаРождения,
		|	Сотрудники.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
		|	Сотрудники.Наименование КАК Наименование
		|ПОМЕСТИТЬ ВТСотрудники
		|ИЗ
		|	Справочник.Сотрудники КАК Сотрудники
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТФизическиеЛица КАК ФизическиеЛица
		|		ПО Сотрудники.ФизическоеЛицо = ФизическиеЛица.Ссылка
		|ГДЕ
		|	Сотрудники.ГоловнойСотрудник = Сотрудники.Ссылка
		|	И НЕ Сотрудники.ВАрхиве
		|	И Сотрудники.ГоловнаяОрганизация = &ГоловнаяОрганизация
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
		|	Сотрудники.Сотрудник КАК Сотрудник,
		|	Сотрудники.Наименование КАК Наименование,
		|	Сотрудники.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
		|	Сотрудники.ДатаРождения КАК ДатаРождения,
		|	Сотрудники.ФизическоеЛицо КАК ФизическоеЛицо
		|ИЗ
		|	ВТСотрудники КАК Сотрудники
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РолиСотрудников КАК РолиРаботники
		|		ПО (РолиРаботники.Сотрудник = Сотрудники.Сотрудник)
		|			И (РолиРаботники.РольСотрудника = ЗНАЧЕНИЕ(Перечисление.РолиСотрудников.Работник))
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ТекущиеКадровыеДанныеСотрудников КАК ТекущиеКадровыеДанныеСотрудников
		|		ПО (ТекущиеКадровыеДанныеСотрудников.Сотрудник = Сотрудники.Сотрудник)
		|			И (ТекущиеКадровыеДанныеСотрудников.ФизическоеЛицо = Сотрудники.ФизическоеЛицо)
		|			И (ТекущиеКадровыеДанныеСотрудников.ГоловнаяОрганизация = Сотрудники.ГоловнаяОрганизация)
		|ГДЕ
		|	РолиРаботники.РольСотрудника ЕСТЬ НЕ NULL 
		|	И ТекущиеКадровыеДанныеСотрудников.ДатаУвольнения = &ДатаУвольнения
		|
		|УПОРЯДОЧИТЬ ПО
		|	Сотрудники.Наименование";	
	
	Если ГоловнаяОрганизация = Неопределено Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "Сотрудники.ГоловнаяОрганизация = &ГоловнаяОрганизация", "ИСТИНА");
	КонецЕсли;
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

Функция ГСИ_ДополнитьСтрокуПробелами(ВхСтрока, Длина)  
	Пробел = " "; //Символ(0);
	Пока СтрДлина(ВхСтрока) < Длина Цикл		
		ВхСтрока = ВхСтрока + Пробел;
	КонецЦикла;
	Возврат ВхСтрока;
КонецФункции  

#КонецОбласти
