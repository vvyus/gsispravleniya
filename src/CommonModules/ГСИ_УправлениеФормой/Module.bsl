#Область ПрограммныйИнтерфейс
// методы используются в ОплатаДнейУходаЗаДетьмиИнвалидами.ФормаДокумента
// для программного создания, заполнения и сохранения таблицы формы в доп ревизит формы
// реквизит создается в режиме предприятия тип -строка имя -"ТаблицаДнейПоМесяцам"
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения:
//   * Элементы - ВсеЭлементыФормы:
Процедура СоздатьТаблицуФормы(Форма) Экспорт 
	Элементы=Форма.Элементы;  
	ИмяТаблицыДобавляемой=ИмяТаблицыДобавляемой();
	
	//добавляем таблицу с двумя колонками
	ДобавляемыеРеквизиты=Новый Массив; 
	
	ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы(ИмяТаблицыДобавляемой,Новый ОписаниеТипов("ТаблицаЗначений"),,"Ранее оплаченные",Истина));//истина изменять сохр данные
	ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы("Период",ОбщегоНазначения.ОписаниеТипаДата(ЧастиДаты.Дата),ИмяТаблицыДобавляемой,"Период",Истина));
	ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы("Дней",ОбщегоНазначения.ОписаниеТипаЧисло(2,0),ИмяТаблицыДобавляемой,"Дней",Истина));
		
	Форма.ИзменитьРеквизиты(ДобавляемыеРеквизиты);  
	
	//таблицу размещаем на ДополнительноСтраница 	
	ГдеРазмещатьГруппу=Элементы.ДополнительноСтраница;
	
	ГруппаПараметров=Элементы.Добавить("ГруппаПараметров",Тип("ГруппаФормы"),ГдеРазмещатьГруппу);
	ГруппаПараметров.Вид= ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаПараметров.Поведение=ПоведениеОбычнойГруппы.Свертываемая;
	ГруппаПараметров.Заголовок="Дни по периодам";
	ГруппаПараметров.ОтображениеУправления= ОтображениеУправленияОбычнойГруппы.Картинка;//не гиперссылка   
	ГруппаПараметров.Группировка= ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	
	// добавлияем таблицу
	ЭлементТаблицы=Элементы.Добавить(ИмяТаблицыДобавляемой,Тип("ТаблицаФормы"),ГруппаПараметров);
	ЭлементТаблицы.ПутьКДанным=ИмяТаблицыДобавляемой;
	ЭлементТаблицы.Отображение= ОтображениеТаблицы.Список;
	
	ЭлементФормы=Элементы.Добавить("Период",Тип("ПолеФормы"),ЭлементТаблицы);
	ЭлементФормы.Вид=ВидПоляФормы.ПолеВвода;
	ЭлементФормы.ПутьКДанным=ИмяТаблицыДобавляемой+"."+"Период";
	ЭлементФормы.УстановитьДействие("ПриИзменении","Подключаемый_ТаблицаДнейПоМесяцамПериодПриИзменении");
	
	ЭлементФормы=Элементы.Добавить("Дней",Тип("ПолеФормы"),ЭлементТаблицы);
	ЭлементФормы.Вид=ВидПоляФормы.ПолеВвода;
	ЭлементФормы.ПутьКДанным=ИмяТаблицыДобавляемой+"."+"Дней";
	
КонецПроцедуры	

// Заполнить таблицу формы.
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения:
//   * Элементы - ВсеЭлементыФормы:
Процедура ЗаполнитьТаблицуФормы(Форма) Экспорт   	
	Объект=Форма.РеквизитФормыВЗначение("Объект");
	ДопРеквизит = РеквизитДляХраненияТаблицы();
	Если ЗначениеЗаполнено(ДопРеквизит) Тогда
		ТаблицаДнейПоМесяцам=Форма[ИмяТаблицыДобавляемой()];	
		ТаблицаДнейПоМесяцам.Очистить(); 
		
		//Считываем значение доп реквизита
		НайденныеСтроки = Объект.ДополнительныеРеквизиты.НайтиСтроки(Новый Структура("Свойство", ДопРеквизит));
		ЗначениеРеквизита = ?(НайденныеСтроки.Количество() = 1, НайденныеСтроки[0].Значение, Неопределено);
		Если ЗначениеРеквизита <> Неопределено Тогда		
			МассивСтрок=СтрРазделить(ЗначениеРеквизита,ПолучитьРазделительСтрок());
			Если МассивСтрок.Количество()>0 Тогда
				Для Каждого СтрокаМассива Из МассивСтрок Цикл
					МассивСтроки=СтрРазделить(СтрокаМассива,ПолучитьРазделительВСтроке());
					Если МассивСтроки.Количество()=2 Тогда
						СтрТз=ТаблицаДнейПоМесяцам.Добавить();
						СтрТз.Период=МассивСтроки[0];
						СтрТз.Дней=МассивСтроки[1];
					КонецЕсли;	
				КонецЦикла;	
			КонецЕсли;	
		КонецЕсли;	
	КонецЕсли;	
	
КонецПроцедуры	 

// Записать таблицу.
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - :
// * Элементы - ВсеЭлементыФормы - 
Процедура ЗаписатьТаблицу(Форма) Экспорт
	//ДопРеквизит=ЭтаФорма[ИмяРеквизитаДляХраненияТаблицы()];
	ДопРеквизит = РеквизитДляХраненияТаблицы();
	Если ЗначениеЗаполнено(ДопРеквизит) Тогда           
		ТаблицаДнейПоМесяцам=Форма[ИмяТаблицыДобавляемой()];	
		//Свойства_ОписаниеДополнительныхРеквизитов.
		НайденныеСтроки = Форма.Свойства_ОписаниеДополнительныхРеквизитов.НайтиСтроки(Новый Структура("Свойство", ДопРеквизит));
		ЗначениеРеквизита="";
		Для Каждого СтрТз Из ТаблицаДнейПоМесяцам Цикл
			ЗначениеРеквизита=ЗначениеРеквизита+СтрТз.Период+ПолучитьРазделительВСтроке()+СтрТз.Дней;
			//новая строка
			ЗначениеРеквизита=ЗначениеРеквизита+ПолучитьРазделительСтрок();
		КонецЦикла; 
		Если ЗначениеЗаполнено(ЗначениеРеквизита) Тогда
			ЗначениеРеквизита=Лев(ЗначениеРеквизита,СтрДлина(ЗначениеРеквизита)-1);
		КонецЕсли;	
		//Если ТЗ изменили записываем новое значение доп реквизита
		Если Форма[НайденныеСтроки[0].ИмяРеквизитаЗначение] <> ЗначениеРеквизита Тогда
			 Форма[НайденныеСтроки[0].ИмяРеквизитаЗначение]  = ЗначениеРеквизита;
			 
		КонецЕсли;	
	КонецЕсли;	
КонецПроцедуры	

// Установить видимость реквизита таблицы.
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - :
// * Элементы - ВсеЭлементыФормы - 
Процедура УстановитьВидимостьРеквизитаТаблицы(Форма) Экспорт
		//ДопРеквизит=ЭтаФорма[ИмяРеквизитаДляХраненияТаблицы()];
	ДопРеквизит = РеквизитДляХраненияТаблицы();
	Если ЗначениеЗаполнено(ДопРеквизит) Тогда
		//Ищем на форме нужный нам доп реквизит
		//Свойства_ОписаниеДополнительныхРеквизитов 
		НайденныеСтроки = Форма.Свойства_ОписаниеДополнительныхРеквизитов.НайтиСтроки(Новый Структура("Свойство", ДопРеквизит));
		Элементы=Форма.Элементы;
		//Прячем элемент программно
		Если НайденныеСтроки.Количество() = 1 Тогда 
			Элементы[НайденныеСтроки[0].ИмяРеквизитаЗначение].Видимость = Ложь;
		КонецЕсли;	
	КонецЕсли;

КонецПроцедуры   

// метод используется в
// ЗаявлениеВФССОВозмещенииВыплатРодителямДетейИнвалидов.МодульМенеджера.ВывестиТаблицуНакопленных_2024 для отчета
// 
// Параметры:
//  ТаблицаСтрокПоСотруднику-ТаблицаЗначений- Таблица строк по сотруднику
//  ТаблицаЗаполнения-ТаблицаЗначений - Таблица заполнения
Процедура ЗаполнитьТаблицуИзДополнительногоРеквизита(ТаблицаСтрокПоСотруднику,ТаблицаЗаполнения) Экспорт
	ДопРеквизит =РеквизитДляХраненияТаблицы(); 
	Если ЗначениеЗаполнено(ДопРеквизит) И ТаблицаСтрокПоСотруднику.Количество() >0 Тогда
		Объект=ТаблицаСтрокПоСотруднику[0].ДокументОснование.ПолучитьОбъект();
		//Считываем значение доп реквизита
		НайденныеСтроки = Объект.ДополнительныеРеквизиты.НайтиСтроки(Новый Структура("Свойство", ДопРеквизит));
		ЗначениеРеквизита = ?(НайденныеСтроки.Количество() = 1, НайденныеСтроки[0].Значение, Неопределено);
		Если ЗначениеРеквизита <> Неопределено Тогда		
			МассивСтрок=СтрРазделить(ЗначениеРеквизита,ПолучитьРазделительСтрок());
			Если МассивСтрок.Количество()>0 Тогда
				Для Каждого СтрокаМассива Из МассивСтрок Цикл
					МассивСтроки=СтрРазделить(СтрокаМассива,ПолучитьРазделительВСтроке());
					Если МассивСтроки.Количество()=2 Тогда
						СтрТз=ТаблицаЗаполнения.Добавить();
						СтрТз.Месяц=МассивСтроки[0];
						СтрТз.НакопленоДнейЗаМесяц=МассивСтроки[1];
						СтрТз.ЕстьНакопленные=Истина;
					КонецЕсли;	
				КонецЦикла;	
			КонецЕсли;	
		КонецЕсли;	
	КонецЕсли;	
	
КонецПроцедуры	


#КонецОбласти


#Область СлужебныеПроцедурыИФункции
Функция ИмяТаблицыДобавляемой()
	Возврат "ТаблицаДнейПоМесяцам";
КонецФункции
//доп реквизит д быть добавлен в режиме предприятия
Функция ИмяРеквизитаДляХраненияТаблицы()
	Возврат "ТаблицаДнейПоМесяцам";
КонецФункции

Функция ПолучитьРазделительСтрок() 
	Возврат ";";
КонецФункции	

Функция ПолучитьРазделительВСтроке() 
	Возврат "^";
КонецФункции	

Функция РеквизитДляХраненияТаблицы() 
	Возврат ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоНаименованию(ИмяРеквизитаДляХраненияТаблицы());
КонецФункции

#КонецОбласти
