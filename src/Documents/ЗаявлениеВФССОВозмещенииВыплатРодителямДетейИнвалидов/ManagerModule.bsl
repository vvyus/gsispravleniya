&Вместо("ВывестиТаблицуНакопленных_2024")
Процедура ГСИ_ВывестиТаблицуНакопленных_2024(КонтекстПечати, ТаблицаСтрокПоСотруднику)
	// Таблица накопленных (неиспользованных) дней за месяц.
	РазрешитьДобавлениеСтраницы(КонтекстПечати.ТабличныйДокумент);
	КонтекстПечати.ТабличныйДокумент.Вывести(КонтекстПечати.Макет.ПолучитьОбласть("НакопленныеДни_Шапка"));
	ВложенныеИзмерения = "Месяц, ЕстьНакопленные";
	ВложенныеРесурсы   = "НакопленоДнейЗаМесяц, НакопленоЧасовЗаМесяц, КоличествоЧасовЗаМесяц";
	Фильтр             = Новый Структура("ЕстьНакопленные", Истина);
	ВложеннаяТаблица = ТаблицаСтрокПоСотруднику.Скопировать(Фильтр, ВложенныеИзмерения + ", " + ВложенныеРесурсы);
	ВложеннаяТаблица.Свернуть(ВложенныеИзмерения, ВложенныеРесурсы);
	ВложеннаяТаблица.Сортировать(ВложенныеИзмерения);
	Если ВложеннаяТаблица.Количество() = 0 Тогда
		//+ГС 27.06.2024 
		ТаблицаПуста=Истина;
		ДопРеквизит = ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоНаименованию(ГСИ_ОбщийМодуль.ПолучитьНаименованиеДопРеквизита());
		Если ЗначениеЗаполнено(ДопРеквизит) И ТаблицаСтрокПоСотруднику.Количество() >0 Тогда
			Объект=ТаблицаСтрокПоСотруднику[0].ДокументОснование.ПолучитьОбъект();
			//Считываем значение доп реквизита
			НайденныеСтроки = Объект.ДополнительныеРеквизиты.НайтиСтроки(Новый Структура("Свойство", ДопРеквизит));
			ЗначениеРеквизита = ?(НайденныеСтроки.Количество() = 1, НайденныеСтроки[0].Значение, Неопределено);
			Если ЗначениеРеквизита <> Неопределено Тогда		
				МассивСтрок=СтрРазделить(ЗначениеРеквизита,ГСИ_ОбщийМодуль.ПолучитьРазделительСтрок());
				Если МассивСтрок.Количество()>0 Тогда
					Для Каждого СтрокаМассива Из МассивСтрок Цикл
						МассивСтроки=СтрРазделить(СтрокаМассива,ГСИ_ОбщийМодуль.ПолучитьРазделительВСтроке());
						Если МассивСтроки.Количество()=2 Тогда
							СтрТз=ВложеннаяТаблица.Добавить();
							СтрТз.Месяц=МассивСтроки[0];
							СтрТз.НакопленоДнейЗаМесяц=МассивСтроки[1];
							СтрТз.ЕстьНакопленные=Истина;
						КонецЕсли;	
					КонецЦикла;	
					ТаблицаПуста=Ложь;
				КонецЕсли;	
			КонецЕсли;	
		КонецЕсли;	
		Если ТаблицаПуста Тогда
		//-ГС 27.06.2024
			ВложеннаяТаблица.Добавить();
			ВложеннаяТаблица.Добавить();
			ВложеннаяТаблица.Добавить();
		КонецЕсли;	
	КонецЕсли;
	
	Для Каждого ВложеннаяСтрока Из ВложеннаяТаблица Цикл
		Секция = УправлениеПечатьюБЗК.СекцияПечатнойФормы(КонтекстПечати, "НакопленныеДни_Строка");
		Если ВложеннаяСтрока.ЕстьНакопленные Тогда
			Параметры = Новый Структура(КонтекстПечати.ИменаПараметровОбластей.НакопленныеДни_Строка);
			Параметры.НакопленныеДни_Месяц = Формат(ВложеннаяСтрока.Месяц, "ДФ=MMMM");
			Год           = Год(ВложеннаяСтрока.Месяц);
			НакопленоДней = ВложеннаяСтрока.НакопленоДнейЗаМесяц;
			УправлениеПечатьюБЗК.ВывестиЧислоПоБуквам(Секция, Год,           "НакопленныеДни_Год");
			УправлениеПечатьюБЗК.ВывестиЧислоПоБуквам(Секция, НакопленоДней, "НакопленоДней");
			Секция.ТабличныйДокумент.Параметры.Заполнить(Параметры);
		КонецЕсли;
		КонтекстПечати.ТабличныйДокумент.Вывести(Секция.ТабличныйДокумент);
	КонецЦикла;
	Секция = УправлениеПечатьюБЗК.СекцияПечатнойФормы(КонтекстПечати, "НакопленныеДни_Подвал");
	НакопленоДнейИтог = ВложеннаяТаблица.Итог("НакопленоДнейЗаМесяц");
	Если НакопленоДнейИтог > 0 Тогда
		УправлениеПечатьюБЗК.ВывестиЧислоПоБуквам(Секция, НакопленоДнейИтог, "НакопленоДнейИтог");
	КонецЕсли;
	НакопленоЧасовИтог = ВложеннаяТаблица.Итог("НакопленоЧасовЗаМесяц");
	Если НакопленоЧасовИтог > 0 Тогда
		УправлениеПечатьюБЗК.ВывестиЧислоПоБуквам(Секция, НакопленоЧасовИтог, "НакопленоЧасовИтог");
	КонецЕсли;
	КонтекстПечати.ТабличныйДокумент.Вывести(Секция.ТабличныйДокумент);
	
КонецПроцедуры

&Перед("ВывестиТаблицуЗаМесяц_2024")
Процедура ГСИ_ВывестиТаблицуЗаМесяц_2024(КонтекстПечати, ТаблицаСтрокПоСотруднику)
	// если в таблице по сотруднику несколько записей то оставлям толко средний заработок первой строки
	Если ТаблицаСтрокПоСотруднику.Количество()>1 Тогда
		НомерСтроки=0;
		Для каждого Стр из ТаблицаСтрокПоСотруднику Цикл
			Если НомерСтроки>0 Тогда
				Стр.ЗаработокЗаМесяц=0;//СреднедневнойЗаработок
			КонецЕсли;	
			НомерСтроки=НомерСтроки+1;
		КонецЦикла;	
	КонецЕсли;	
	
КонецПроцедуры

