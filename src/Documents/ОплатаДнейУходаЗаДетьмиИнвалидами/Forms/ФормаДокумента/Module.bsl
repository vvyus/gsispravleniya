
&НаСервере
Процедура ГСИ_ПриСозданииНаСервереПосле(Отказ, СтандартнаяОбработка)
	ДопРеквизит = ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоНаименованию(ГСИ_ОбщийМодуль.ПолучитьНаименованиеДопРеквизита());
	Если ЗначениеЗаполнено(ДопРеквизит) Тогда
		Элементы.ГруппаДниПоМесяцам.Видимость = Истина;
		//Считываем значение доп реквизита
		НайденныеСтроки = Объект.ДополнительныеРеквизиты.НайтиСтроки(Новый Структура("Свойство", ДопРеквизит));
		ЗначениеРеквизита = ?(НайденныеСтроки.Количество() = 1, НайденныеСтроки[0].Значение, Неопределено);
		Если ЗначениеРеквизита <> Неопределено Тогда		
			МассивСтрок=СтрРазделить(ЗначениеРеквизита,ГСИ_ОбщийМодуль.ПолучитьРазделительСтрок());
			Если МассивСтрок.Количество()>0 Тогда
				Для Каждого СтрокаМассива Из МассивСтрок Цикл
					МассивСтроки=СтрРазделить(СтрокаМассива,ГСИ_ОбщийМодуль.ПолучитьРазделительВСтроке());
					Если МассивСтроки.Количество()=2 Тогда
						СтрТз=ТаблицаДнейПоМесяцам.Добавить();
						СтрТз.Период=МассивСтроки[0];
						СтрТз.Количество=МассивСтроки[1];
					КонецЕсли;	
				КонецЦикла;	
			КонецЕсли;	
		КонецЕсли;	
	КонецЕсли;	
КонецПроцедуры

&НаСервере
Процедура ГСИ_ПередЗаписьюНаСервереПеред(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	Если ЗначениеЗаполнено(ДопРеквизит) Тогда
		//Свойства_ОписаниеДополнительныхРеквизитов - В документообороте, в других конфигурациях скорее всего по другому.
		НайденныеСтроки = ЭтаФорма.Свойства_ОписаниеДополнительныхРеквизитов.НайтиСтроки(Новый Структура("Свойство", ДопРеквизит));
		ЗначениеРеквизита="";
		Для Каждого СтрТз Из ТаблицаДнейПоМесяцам Цикл
			ЗначениеРеквизита=ЗначениеРеквизита+СтрТз.Период+ГСИ_ОбщийМодуль.ПолучитьРазделительВСтроке()+СтрТз.Количество;
			//новая строка
			ЗначениеРеквизита=ЗначениеРеквизита+ГСИ_ОбщийМодуль.ПолучитьРазделительСтрок();
		КонецЦикла; 
		Если ЗначениеЗаполнено(ЗначениеРеквизита) Тогда
			ЗначениеРеквизита=Лев(ЗначениеРеквизита,СтрДлина(ЗначениеРеквизита)-1);
		КонецЕсли;	
		//Если ТЗ изменили записываем новое значение доп реквизита
		Если ЭтаФорма[НайденныеСтроки[0].ИмяРеквизитаЗначение] <> ЗначениеРеквизита Тогда
			 ЭтаФорма[НайденныеСтроки[0].ИмяРеквизитаЗначение]  = ЗначениеРеквизита;
			 
		КонецЕсли;	
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ГСИ_ПриОткрытииПосле(Отказ)
	Если ЗначениеЗаполнено(ДопРеквизит) Тогда
		//Ищем на форме нужный нам доп реквизит
		//Свойства_ОписаниеДополнительныхРеквизитов - В документообороте, в других конфигурациях скорее всего по другому.
		НайденныеСтроки = ЭтаФорма.Свойства_ОписаниеДополнительныхРеквизитов.НайтиСтроки(Новый Структура("Свойство", ДопРеквизит));
		//Прячем элемент программно
		Если НайденныеСтроки.Количество() = 1 Тогда 
			Элементы[НайденныеСтроки[0].ИмяРеквизитаЗначение].Видимость = Ложь;
		КонецЕсли;	
	КонецЕсли;
КонецПроцедуры
